apply plugin: 'com.android.application'
apply from: 'env.gradle'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    signingConfigs {
        release {
            storeFile file("expo_hub_key.jks")
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    defaultConfig {

        applicationId "com.ericdesedas.expohub"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 5
        versionName "0.2.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Google Api Key
        resValue "string", "google_maps_key", GOOGLE_MAPS_API_KEY

        // General Api Parameters
        resValue "string", "api_key", API_KEY
        resValue "string", "base_url", (project.findProperty("BASE_URL") ?: "")
        resValue "string", "api_base_url", (project.findProperty("API_BASE_URL") ?: "")
        resValue "integer", "read_timeout", (project.findProperty("READ_TIMEOUT") ?: "")
        resValue "integer", "write_timeout", (project.findProperty("WRITE_TIMEOUT") ?: "")

        // App Specific Parameters
        resValue "string", "shared_pref_file", (project.findProperty("SHARED_PREF_FILE") ?: "")
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
        }
        release {
            minifyEnabled false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Testing Support Libraries
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.0.42-beta'
    testImplementation 'org.powermock:powermock-module-junit4:1.6.6'
    testImplementation 'org.powermock:powermock-api-mockito2:1.6.6'
    testImplementation 'org.hamcrest:hamcrest-junit:2.0.0.0'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // Android related Libraries
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.gms:play-services-maps:17.0.0'

    // ButterKnife
    implementation 'com.jakewharton:butterknife:10.2.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.0'

    // IcePick
    implementation 'frankiesardo:icepick:3.2.0'
    annotationProcessor 'frankiesardo:icepick-processor:3.2.0'

    // Moshi / Retrofit
    implementation 'moe.banana:moshi-jsonapi:3.0.4'
    implementation 'com.squareup.retrofit2:converter-moshi:2.6.2'
    implementation 'com.squareup.retrofit2:retrofit:2.6.2'

    // Picasso
    implementation 'com.squareup.picasso:picasso:2.71828'

    // Apache Commons
    implementation 'commons-io:commons-io:20030203.000550'
    implementation 'org.apache.commons:commons-lang3:3.9'

    // Dagger
    implementation 'com.google.dagger:dagger:2.24'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.24'
    implementation 'org.glassfish:javax.annotation:10.0-b28'

    // Event Bus
    implementation 'org.greenrobot:eventbus:3.0.0'

    // Joda Time
    implementation 'joda-time:joda-time:2.9.6'
}
